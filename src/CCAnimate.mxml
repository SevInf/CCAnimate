<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   xmlns:gnat="com.goodnamesaretaken.CCAnimate.*"
					   minWidth="800"
					   minHeight="600"
					   >
	<fx:Declarations>
		<fx:XML format="e4x" id="menuData">
			<root>
				<menuitem label="File">
					<menuitem id="new" keyEquivalent="n" commandKey="true" label="_New project"/>
					<menuitem id="open" keyEquivalent="o" commandKey="true" label="_Open project" />
					<menuitem type="separator" />
					<menuitem id="save" keyEquivalent="s" commandKey="true" label="_Save project"/>
					<menuitem id="saveas" keyEquivalent="s" commandKey="true" shiftKey="true" label="Save project as..." enabled="false" />
					<menuitem type="separator" />
					<menuitem id="export" keyEquivalent="e" commandKey="true" label="_Export animations" />
				</menuitem>
			</root>
		</fx:XML>
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import com.goodnamesaretaken.CCAnimate.Animation;
			import com.goodnamesaretaken.CCAnimate.Frame;
			import com.goodnamesaretaken.util.PlistUtils;
			
			import mx.collections.ArrayList;
			import mx.events.FlexNativeMenuEvent;
			
			[Bindable]
			private var _animations: ArrayList = new ArrayList();
			
			[Bindable]
			private var _library: ArrayList = new ArrayList();
			
			
			private function addImagesToLibrary(e: Event):void {
				var file: File = new File();
				
				file.browseForOpenMultiple("Choose file to add", [new FileFilter("PNG Files", "png")]);
				file.addEventListener(FileListEvent.SELECT_MULTIPLE, imageFilesSelected);
			
			}
			
			private function removeImageFromLibrary(e: Event):void {
				for each (var frame: Frame in libraryList.selectedItems) {
					_library.removeItem(frame);	
				}
			}
			
			
			private function addAnimation(e: Event): void {
				var animation:Animation = new Animation();
				animation.name = "Animation " + (_animations.length + 1);
				
				_animations.addItem(animation);
				animationsList.selectedItem = animation;
			}
			
			private function removeAnimation(e: Event): void {
				_animations.removeItem(animationsList.selectedItem);
			}
			
			private function imageFilesSelected(e: FileListEvent):void {
				for each (var f: File in e.files) { 
					_library.addItem(Frame.fromFile(f));
				}
			}
			
			
			private function menuItemClick(event:FlexNativeMenuEvent): void {
				var itemId:String = event.item.@id;
				if (itemId == "new") {
					newProject();
				} if (itemId == "save") {
					var saveFile:File = new File();
					saveFile.addEventListener(Event.SELECT, saveProjectFileSelected);
					saveFile.browseForSave("Save project");
				} else if (itemId == "open") {
					var openFile:File = new File();
					openFile.addEventListener(Event.SELECT, openProjectFileSelected);
					openFile.browseForOpen("Open project");
				} else if (itemId == "export") {
					var exportFile: File = new File();
					exportFile.addEventListener(Event.SELECT, exportProjectFileSelected);
					exportFile.browseForSave("Export animation for cocos2d");
				}
				
			}
			
			private function serializeProjectAsXML(projectFileDirectory: File): XML {
				var output:XML = <project></project>;
				var libraryXML: XML = <library />;
				
				for each (var frame: Frame in _library.source) {
					var item: XML = <file />;
					item.@path = projectFileDirectory.getRelativePath(new File(frame.originalPath));
					libraryXML.appendChild(item);
				}
				output.appendChild(libraryXML);
				
				var animationsXML: XML = <animations/>;
				for each (var animation: Animation in _animations.source) {
					var animationXML: XML = <animation />;
					animationXML.@name = animation.name;
					animationXML.@interval = animation.frameInterval;
					
					for each (var _frame: Frame in animation.frames.source) {
						var frameXML: XML = <frame />;
						frameXML.@name = _frame.name;
						animationXML.appendChild(frameXML);
					}
					
					animationsXML.appendChild(animationXML);
				}
				output.appendChild(animationsXML);
				return output;
			}
			
			private function libraryFrameByName(frameName: String): Frame {
				for each (var frame: Frame in _library.source) {
					if (frame.name == frameName) {
						return frame;
					}
				}
				
				return null;
			}
			
			private function deserializeProjectFromXML(project: XML, projectFileDirectory: File): void {
				
				for each (var file:XML in project.library.file) {
					var imageFile: File = projectFileDirectory.resolvePath(file.@path);
					_library.addItem(Frame.fromFile(imageFile));
				}
				
				for each (var animationXML: XML in project.animations.animation) {
					var animation:Animation = new Animation();
					animation.name = animationXML.@name;
					animation.frameInterval = animationXML.@interval;
					
					for each (var frameXML:XML in animationXML.frame) {
						animation.frames.addItem(libraryFrameByName(frameXML.@name));
					}
					_animations.addItem(animation);
				}
			}
			
			private function newProject(): void {
				_library.removeAll();
				_animations.removeAll();
			}
			
			private function saveProjectFileSelected(event: Event):void {
				var data: XML = serializeProjectAsXML(File(event.target.parent));
				var fileStream:FileStream = new FileStream();
			
				fileStream.open(File(event.target), FileMode.WRITE);

				fileStream.writeUTFBytes('<?xml version="1.0" encoding="UTF-8"?>\n');
				fileStream.writeUTFBytes(data.toXMLString());
				fileStream.close();
			}
			
			private function openProjectFileSelected(event: Event): void {
				
				newProject();
				
				var fileStream: FileStream = new FileStream();
				fileStream.open(File(event.target), FileMode.READ);
				var xmlString:String = fileStream.readUTFBytes(fileStream.bytesAvailable);
				fileStream.close();
				
				var data:XML = new XML(xmlString);
				deserializeProjectFromXML(data, File(event.target).parent);
			}
			
			private function exportProjectFileSelected(event: Event): void {
				var exportAnimations: Object = {};
				for each (var animation: Animation in _animations.source) {
					var exportObject: Object = {
						delay: animation.frameInterval,
						frames:[]
					};
					
					for each (var frame: Frame in animation.frames.source) {
						exportObject.frames.push(frame.name);
					}
					exportAnimations[animation.name] = exportObject;
				}
				PlistUtils.saveObject(exportAnimations, File(event.target));
			}
			
			
			
		]]>
	</fx:Script>
	
	<s:menu>
		<mx:FlexNativeMenu dataProvider="{menuData}" 
						   labelField="@label" 
						   keyEquivalentField="@keyEquivalent" 
						   showRoot="false" 
						   itemClick="menuItemClick(event)" />
			
	</s:menu>
	
	<s:layout>
		<s:VerticalLayout />
	</s:layout>
	
	<s:Group height="70%" width="100%">
		<s:layout>
			<s:HorizontalLayout />
		</s:layout>
		
		<s:Panel height="100%" width="20%" minWidth="250" title="Animations">
			<s:layout>
				<s:VerticalLayout />
			</s:layout>
			
			<s:controlBarContent>
				<s:Button label="Add animation" click="addAnimation(event)" />
				<s:Button label="Remove animation" click="removeAnimation(event)" enabled="{animationsList.selectedItem != null}" />
			</s:controlBarContent>
			
			<s:List id="animationsList" 
					width="100%" height="100%" 
					dataProvider="{_animations}" 
					itemRenderer="com.goodnamesaretaken.CCAnimate.AnimationListItemRenderer">
				
				<s:layout>
					<s:VerticalLayout paddingTop="5" paddingLeft="5" paddingRight="5" paddingBottom="5"/>		
				</s:layout>
				
			</s:List>
		</s:Panel>
		
		<gnat:AnimationEditor animation="{animationsList.selectedItem}" width="80%" height="100%" />
		
	</s:Group>
	
	<s:Panel title="Frames Library" width="100%" height="30%">
		<s:controlBarContent>
			
			<s:Button label="Add Image" click="addImagesToLibrary(event)" />
			
			<s:Button label="Remove Image" 
					  click="removeImageFromLibrary(event)" 
					  enabled="{libraryList.selectedItem != null}" />
			
		</s:controlBarContent>
		
		<s:List id="libraryList"
				dataProvider="{_library}" width="100%" height="100%" 
				itemRenderer="com.goodnamesaretaken.CCAnimate.LibraryItemRenderer"
				dragEnabled="true" allowMultipleSelection="true">
			
			<s:layout>
				<s:HorizontalLayout gap="10" verticalAlign="bottom" 
									paddingTop="10" paddingBottom="10" 
									paddingLeft="10" paddingRight="10"/>
			</s:layout>
		</s:List>
	</s:Panel>
	
	
</s:WindowedApplication>
